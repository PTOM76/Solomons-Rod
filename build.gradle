plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "0.12.0-SNAPSHOT" apply false
    id 'com.matthewprenger.cursegradle' version '1.+'
    id "com.modrinth.minotaur" version "2.+"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "com.modrinth.minotaur"
    apply plugin: "com.matthewprenger.cursegradle"

    loom {
        silentMojangMappingsLicense()
    }

    repositories {
        maven { url "https://maven.pkom.ml/" }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        //mappings loom.officialMojangMappings()
        // The following line declares the yarn mappings you may select this one as well.
        mappings "net.fabricmc:yarn:1.20+build.1:v2"

        modApi "ml.pkom:mcpitanlibarch-common${rootProject.mcpitanlib_version}"
    }

    if (name in "fabric,forge".split(",")) {
        var modLoader = name

        if (System.getenv("CURSEFORGE_TOKEN") != null) {
            curseforge {
                apiKey = System.getenv("CURSEFORGE_TOKEN")
                project {
                    id = '519836'
                    changelog = 'Support 1.20'
                    if (modLoader == "fabric") {
                        releaseType = 'release'
                        addGameVersion "Fabric"
                    }
                    if (modLoader == "forge") {
                        releaseType = 'beta'
                        addGameVersion "Forge"
                    }
                    addGameVersion '1.17'
                    addGameVersion '1.17.1'
                    addGameVersion '1.18'
                    addGameVersion '1.18.1'
                    addGameVersion '1.18.2'
                    addGameVersion '1.19'
                    addGameVersion '1.19.1'
                    addGameVersion '1.19.2'
                    addGameVersion '1.19.3'
                    addGameVersion '1.20'
                    relations {
                        if (modLoader == "fabric") {
                            requiredDependency "fabric-api"
                        }
                        requiredDependency 'mcpitanlibarch'
                    }

                    mainArtifact(tasks.remapJar)
                }
                options {
                    forgeGradleIntegration = false
                }
            }

        }

        if (System.getenv("MODRINTH_TOKEN") != null) {
            modrinth {
                token = System.getenv("MODRINTH_TOKEN")
                projectId = 'dRsm10gb'
                versionNumber = project.mod_version + "-" + modLoader
                gameVersions = ['1.17', '1.17.1', '1.18', '1.18.1', '1.18.2', '1.19', '1.19.1', '1.19.2', '1.19.3', '1.20']
                versionType = 'release'
                uploadFile = tasks.remapJar
                changelog = 'Support 1.20'
                loaders = [modLoader]
                dependencies {
                    if (modLoader == "fabric") {
                        required.project "P7dR8mSH" // Fabric API
                    }
                    required.project "uNRoUnGT" // MCPitanLibarch
                }
            }
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 8
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    java {
        withSourcesJar()
    }
}
